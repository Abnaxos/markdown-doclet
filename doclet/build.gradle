apply plugin:OSSRH

archivesBaseName = 'markdown-doclet'

dependencies {
    compile libs.pegdown
    compile libs.guava
    compile libs.plantuml
    compileOnly libs.jdkTools

    // GIST MARKDOWN TAGLET
    compile libs.github
    compileOnly libs.findbugs
    compile libs.okhttp
    compile libs.velocity

    testCompile group: 'org.jsoup', name: 'jsoup', version: '1.8.3'
}

jar {
    dependsOn project(':integrations:gradle-plugin').tasks.build

    into('/') {
        from { zipTree(project(':integrations:gradle-plugin').tasks.jar.archivePath) }
        exclude 'META-INF/MANIFEST.MF'
    }
}

task allJar(type: Jar, dependsOn: jar) {
    classifier = 'all'
    from {
        zipTree(jar.archivePath)
    }
    from {
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
artifacts {
    archives allJar
}

sourceSets.test.java.srcDir 'src/test/javadoc'
