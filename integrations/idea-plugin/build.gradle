configurations {
    packaged
}

version = componentBaseVersion + '-1' + snapshot(false)

dependencies {
    compile docletProject

    compileOnly files(
            ['openapi', 'extensions', 'idea', 'util', 'annotations', 'forms_rt'].
                    collect { baseName -> "$ideaHome/lib/${baseName}.jar" })
    compileOnly libs.jdkTools

    packaged docletProject
}

processResources {
    //noinspection UnnecessaryQualifiedReference
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            "idea-plugin-version": project.version
    ]
}

tasks.jar {
    exclude 'com/intellij/uiDesigner/**'
    configurations.packaged.collect { p ->
        from(zipTree(p)) {
            exclude 'META-INF/**'
        }
    }
}
